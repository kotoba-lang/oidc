(ns oidc.adapters.jwt-rsa-test
  (:require [clojure.test :refer [deftest is]]
            [oidc.adapters.jwks :as jwks]
            [oidc.adapters.jwt-rsa :as rsa]
            [oidc.core :as c]
            [oidc.model :as m]))

(deftest verifies-rs256-id-token-from-jwks
  (let [kp (rsa/generate-rsa-keypair)
        jwk (rsa/jwk-from-public-key (.getPublic kp) {:kid "kid-1"})
        token (rsa/sign-rs256 (.getPrivate kp)
                              {:alg "RS256" :kid "kid-1" :typ "JWT"}
                              {:iss "https://idp.example"
                               :aud "client-1"
                               :sub "alice"
                               :nonce "n1"})
        verifier (rsa/rs256-verifier {:keys [jwk]} {})
        port (jwks/verifier-port verifier {})
        req (m/auth-request "oidc-rsa-1" {:issuer "https://idp.example"
                                          :client-id "client-1"
                                          :nonce "n1"})]
    (is (= "alice" (:oidc.id-token/subject (c/verify port req token))))))

(deftest rejects-invalid-rs256-signature
  (let [good-kp (rsa/generate-rsa-keypair)
        bad-kp (rsa/generate-rsa-keypair)
        jwk (rsa/jwk-from-public-key (.getPublic good-kp) {:kid "kid-1"})
        token (rsa/sign-rs256 (.getPrivate bad-kp)
                              {:alg "RS256" :kid "kid-1"}
                              {:iss "https://idp.example"
                               :aud "client-1"
                               :sub "alice"
                               :nonce "n1"
                               :exp 4102444800})
        verifier (rsa/rs256-verifier {:keys [jwk]} {})
        out (jwks/verify-jwt! verifier token {:issuer "https://idp.example"
                                              :audience "client-1"
                                              :now 100})]
    (is (= :invalid-signature (:error out)))))

(deftest rejects-rs256-token-with-missing-kid
  (let [kp (rsa/generate-rsa-keypair)
        jwk (rsa/jwk-from-public-key (.getPublic kp) {:kid "kid-1"})
        token (rsa/sign-rs256 (.getPrivate kp)
                              {:alg "RS256" :kid "kid-2"}
                              {:iss "https://idp.example"
                               :aud "client-1"
                               :sub "alice"
                               :nonce "n1"})
        verifier (rsa/rs256-verifier {:keys [jwk]} {})
        out (jwks/verify-jwt! verifier token {:issuer "https://idp.example"
                                              :audience "client-1"})]
    (is (= :missing-jwk (:error out)))))
