(ns oidc.adapters.jwks-test
  (:require [clojure.test :refer [deftest is]]
            [oidc.adapters.jwks :as a]
            [oidc.core :as c]
            [oidc.model :as m]))

(deftest verifies-id-token-through-jwks-verifier
  (let [calls (atom [])
        verifier (reify a/IJwksVerifier
                   (verify-jwt! [_ token-ref opts]
                     (swap! calls conj [token-ref opts])
                     {:iss "https://idp.example"
                      :aud ["client-1"]
                      :sub "alice"
                      :nonce "n1"
                      :jwt-ref token-ref}))
        port (a/verifier-port verifier {:jwks-uri "https://idp.example/jwks"
                                        :algorithms #{:RS256}})
        req (m/auth-request "o1" {:issuer "https://idp.example"
                                  :client-id "client-1"
                                  :nonce "n1"})]
    (is (= {:oidc.id-token/ok? true
            :oidc.id-token/issuer "https://idp.example"
            :oidc.id-token/audience "client-1"
            :oidc.id-token/subject "alice"
            :oidc.id-token/nonce "n1"
            :oidc.id-token/evidence-ref "kagi://jwt/id-token"}
           (c/verify port req "kagi://jwt/id-token")))
    (is (= [["kagi://jwt/id-token"
             {:issuer "https://idp.example"
              :audience "client-1"
              :jwks-uri "https://idp.example/jwks"
              :algorithms #{:RS256}}]]
           @calls))))
